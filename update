# ─────────────────────────────────────────────────── #
#                    ██ ▄█▀  ██████                   #
#                    ██▄█▒ ▒██    ▒                   #
#                   ▓███▄░ ░ ▓██▄                     #
#                   ▓██ █▄   ▒   ██▒                  #
#                   ▒██▒ █▄▒██████▒▒                  #
#                   ▒ ▒▒ ▓▒▒ ▒▓▒ ▒ ░                  #
#                   ░ ░▒ ▒░░ ░▒  ░ ░                  #
#                   ░ ░░ ░ ░  ░  ░                    #
#                   ░  ░         ░                    #
# ─────────────────────────────────────────────────── #
options:
#	Nome: KhSkins | Autor: Kick Holse
	version: 1.1
# ──────────────────────────────────────────────────────────────────────────────────────────────────────────────── #
# events
join:
	khcore_notifyUpdate(player, "KhSkins", "khskins", "90189", "{@version}", "khskins atualizar")
quit:
	delete {khskins_p::chat::%player%}
chat:
	{khskins_p::chat::%player%} is true:
		cancel event
		delete {khskins_p::chat::%player%}
		message is "cancelar" or "cancel":
			send yaml value "messages.command.skin.event_cancelled" from "khskins.language"
		else:
			make player execute command "skin %message%"

# ──────────────────────────────────────────────────────────────────────────────────────────────────────────────── #
# commands
command khskins [<text = help>]:
	permission: khskins.*
	permission message: §3[KhSkins] §7v{@version} §f- §7Criado por §6Kick Holse§7.
	trigger:
		arg 1 == "help":
			send "%nl%&e  Ajuda - KhSkins 1/1%nl% "
			send "&3 /skin &f- &7Comando princpal"
			send "&3 /khskins atualizar &f- &7Atualiza o skript%nl% "
		arg 1 == "atualizar":
			khcore update player {khskins::check_update} "KhSkins" script
command skin [<text>]:
	trigger:
		arg 1 is not set:
			khskins_getStats(player)
			set {_skins::*} to skutil yaml list "%uuid of player%.skins" from file "plugins/KhSkins/players/%player%.yml"
			khskins_paggedMenu(player, {_skins::*})
		arg 1 is set
		arg 1 is "help" or "ajuda":
			loop skutil yaml list "messages.command.help" from file "plugins/KhSkins/language.yml":
				send "%loop-value%"
		else if arg 1 is "update" or "atualizar":
			close player's inventory
			send yaml value "messages.command.skin.updated" from "khskins.language"
			make console execute command "sr set %player% %player%"
			set yaml value "%uuid of player%.selected" of file "plugins/KhSkins/players/%player%.yml" to player's displayname
		else if khskins_verifyPlayer(arg 1 parsed as offlineplayer) is true:
			set {_player} to khskins_getPlayerName(arg 1 parsed as offlineplayer) parsed as offlineplayer
			set {_x} to yaml value "messages.command.skin.change" from "khskins.language"
			{roleName.%{_player}%} is set:
				replace "{nick}" with khcore_roles_getColored({_player}) in {_x}
			replace "{nick}" with "%{_player}%" in {_x}
			send {_x}
			make console execute command "sr set %player% %{_player}%"
			set yaml value "%uuid of player%.selected" of file "plugins/KhSkins/players/%player%.yml" to "%{_player}%"
			khcore_listContains(yaml list "%uuid of player%.skins" from file "plugins/KhSkins/players/%player%.yml", "%{_player}%") is not true:
				add "%{_player}%" to yaml list "%uuid of player%.skins" from file "plugins/KhSkins/players/%player%.yml"
		else:
			send yaml value "messages.command.skin.not_found" from "khskins.language"
		
# ──────────────────────────────────────────────────────────────────────────────────────────────────────────────── #
# filemanager
function khskins_filemanager():
	khcore downgit "KhSkins/language.yml" "https://raw.githubusercontent.com/KickHolse/KhSkins/main/files/language"
	loop currently loaded yaml files:
		loop-value contains "khskins.language"
		unload yaml loop-value
	load yaml "plugins/KhSkins/language.yml" as "khskins.language"

# ──────────────────────────────────────────────────────────────────────────────────────────────────────────────── #
# functions
function khskins_paggedMenu(p: player, var: objects, page: number = 0):
	set {_pageStart} to 21*{_page}
	set {_i} to 1
	set {_slot} to 10
	open chest inventory with 6 rows named yaml value "menu.title" from "khskins.language" to {_p}
	wait a tick
	loop skutil yaml nodes "menu.items" from file "plugins/KhSkins/language.yml":
		set {_item} to yaml value "menu.items.%loop-value%.icon" from "khskins.language"
		set {_lore} to khcore_getLore(yaml value "menu.items.%loop-value%.icon" from "khskins.language")
		{_item} contains "{value}":
			set {_o} to yaml value "menu.items.%loop-value%.ownerid" from "khskins.language"
			set {_v} to yaml value "menu.items.%loop-value%.value" from "khskins.language"
			replace "{value}" with "%{_o}%:%{_v}%" in {_item}
		set {_item} to khcore_getItem({_item})
		set {_cmd} to yaml value "menu.items.%loop-value%.execute" from "khskins.language"
		make a gui slot yaml value "menu.items.%loop-value%.slot" from "khskins.language" of {_p} with {_item} without nbt named khcore_getName(yaml value "menu.items.%loop-value%.icon" from "khskins.language") with lore {_lore} to run:
			khskins_ext({_p}, {_cmd})
	loop {_var::*}:
		(loop-index parsed as integer) > {_pageStart}
		set {_loop} to loop-value parsed as offlineplayer
		set {_lore} to replacer "{nick}" with "%{_loop}%" in yaml value "menu.lore" from "khskins.language"
		{_loop} is not {_p}' displayname:
			replace "{right-click}" with yaml value "menu.replaces.right-click" from "khskins.language" in {_lore}
		yml value "%uuid of {_p}%.selected" from file "plugins/KhSkins/players/%{_p}%.yml" is loop-value:
			replace "{state}" with yaml value "menu.replaces.selected" from "khskins.language" in {_lore}
		replace "{state}" with yaml value "menu.replaces.select" from "khskins.language" in {_lore}
		replace "{right-click}" with "" in {_lore}
		make a gui slot {_slot} of {_p} with skull of "%loop-value%" parsed as offlineplayer without nbt named "&a%{_loop}%" with lore replacer "\n" with "||" in {_lore} to run:
			"%click type%" contains "RIGHT":
				{_loop} is not {_p}' displayname
				yml value "%uuid of {_p}%.selected" from file "plugins/KhSkins/players/%{_p}%.yml" is {_loop}:
					make {_p} execute command "skin %{_p}'s displayname%"
				remove "%{_loop}%" from yaml list "%uuid of {_p}%.skins" from file "plugins/KhSkins/players/%{_p}%.yml"
				make {_p} execute command "skin"
			else:
				make {_p} execute command "skin %{_loop}%"
				close {_p}'s inventory
		add 1 to {_slot}
		{_slot} is 17 or 26:
			add 2 to {_slot}
		if {_slot} = ((35*{_i})):
			exit loop
	if (amount of {_var::*}) > {_pageStart} + 21:
		make a gui slot 26 of {_p} with arrow named khcore_getName(yaml value "options.pagged.next_page" from "khskins.language") with lore replacer "{next_page}" with "%{_page} + 1 + 1%" in khcore_getLore(yaml value "options.pagged.next_page" from "khskins.language") to run:
			khskins_paggedMenu({_p}, {_var::*}, ({_page} + 1))
	if {_page} > 0:
		make a gui slot 18 of {_p} with arrow named khcore_getName(yaml value "options.pagged.previous_page" from "khskins.language") with lore replacer "{previous_page}" with "%{_page} + 1 - 1%" in khcore_getLore(yaml value "options.pagged.previous_page" from "khskins.language") to run:
			khskins_paggedMenu({_p}, {_var::*}, ({_page} - 1))
function khskins_getStats(p: player):
	file "plugins/KhSkins/players/%{_p}%.yml" doesn't exist:
		create file "plugins/KhSkins/players/%{_p}%.yml"
		set yml value "%uuid of {_p}%.name" of file "plugins/KhSkins/players/%{_p}%.yml" to {_p}'s displayname
		set yml value "%uuid of {_p}%.selected" of file "plugins/KhSkins/players/%{_p}%.yml" to {_p}'s displayname
		add {_p}'s displayname to yaml list "%uuid of {_p}%.skins" from file "plugins/KhSkins/players/%{_p}%.yml"
function khskins_ext(p: player, cmd: text):
	{_cmd} contains "SKIN>":
		set {_temp::*} to {_cmd} split at "SKIN>"
		make {_p} execute command "skin %{_temp::2}%"
		close {_p}'s inventory
	{_cmd} contains "chat":
		close {_p}'s inventory
		send replacer "\n" with newline in yaml value "messages.command.skin.event" from "khskins.language" to {_p}
		set {khskins_p::chat::%{_p}%} to true
function khskins_getPlayerName(p: offlineplayer) :: text:
	set {_x::*} to text from "https://api.mojang.com/users/profiles/minecraft/%{_p}%" split at """"
	return {_x::4}
function khskins_verifyPlayer(p: offlineplayer) :: boolean:
	text from "https://api.mojang.com/users/profiles/minecraft/%{_p}%" is "":
		return false
	else:
		return true

# ──────────────────────────────────────────────────────────────────────────────────────────────────────────────── #
# load
load:
	khskins_filemanager()
	delete {khskins::check_update}
	wait 1 second
	text from "https://api.spigotmc.org/legacy/update.php?resource=90189" != "{@version}":
		set {khskins::check_update} to true
		send "&3[KHSKINS] &cYou are not using the latest version of skript" to console
	else:
		send "&3[KHSKINS] &aYou are using the latest version of skript" to console
	send "&3[KHSKINS] &aKhSkins &8v{@version} &aenabled!" to console
