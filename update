# ─────────────────────────────────────────────────── #
#                    ██ ▄█▀  ██████                   #
#                    ██▄█▒ ▒██    ▒                   #
#                   ▓███▄░ ░ ▓██▄                     #
#                   ▓██ █▄   ▒   ██▒                  #
#                   ▒██▒ █▄▒██████▒▒                  #
#                   ▒ ▒▒ ▓▒▒ ▒▓▒ ▒ ░                  #
#                   ░ ░▒ ▒░░ ░▒  ░ ░                  #
#                   ░ ░░ ░ ░  ░  ░                    #
#                   ░  ░         ░                    #
# ─────────────────────────────────────────────────── #
options:
	KhSkins | crafted by Kick Holse
	version: 1.4
# ─────────────────────────────────────────────────── #
join:
	getStats(player)

# ─────────────────────────────────────────────────── #
command skin [<text=help>] [<text>] [<text>] [<text>]:
	trigger:
		arg 1 is "help":
			menu(player)
		else:
			arg 2 is set:
				player has permission "khskins.cmd.add" or "khskins.cmd.*" or "khskins.*":
					arg 1 is "add":
						arg 4 is set:
							getName(arg 2 parsed as offlineplayer) is not "khskins->cracked":
								yaml list "skins" from file "plugins/KhSkins/library.yml" contains "%getName(arg 2 parsed as offlineplayer)% : %arg 4% : %arg 3%":
									send "&cThis skin already exists in the library."
								else:
									add "%getName(arg 2 parsed as offlineplayer)% : %arg 4% : %arg 3%" to yaml list "skins" from file "plugins/KhSkins/library.yml"
									send "&aSkin added to library successfully."
							else:
								send "&cWe couldn't find this user in the mojang database."
						else:
							send "&cUse /skin add [nick] [genre] [name]"
				else:
					send "&cYou are not allowed to use this command."
			else:
				player has permission "khskins.cmd.skin" or "khskins.cmd.*" or "khskins.*":
					set {_skins::*} to yaml list "%uuid of player%.skins" from file "plugins/KhSkins/players/%uuid of player%.yml"
					size of {_skins::*} is not 21:
						{_skins::*} contains arg-1:
							send yaml value "messages.command.skin.already_has" from "ks.skins.language"
						else:
							arg 1 is not "%player%":
								getName(arg 1 parsed as offlineplayer) is not "khskins->cracked":
									add getName(arg 1 parsed as offlineplayer) to yaml list "%uuid of player%.skins" from file "plugins/KhSkins/players/%uuid of player%.yml"
									set yaml value "%uuid of player%.select" from file "plugins/KhSkins/players/%uuid of player%.yml" to getName(arg 1 parsed as offlineplayer)
									make console execute command "sr set %player% %arg-1%"
									send yaml value "messages.command.skin.change" from "ks.skins.language"
								else:
									send yaml value "messages.command.skin.not_found" from "ks.skins.language"
							else:
								send yaml value "messages.command.skin.your_skin" from "ks.skins.language"
					else:
						send yaml value "messages.command.skin.no_space" from "ks.skins.language"
				else:
					send yaml value "messages.command.skin.no_permission" from "ks.skins.language"
function menu(p: player, m: text="main"):
	set {_skins::*} to yaml list "%uuid of {_p}%.skins" from file "plugins/KhSkins/players/%uuid of {_p}%.yml"
	set {_selected} to yaml value "%uuid of {_p}%.select" from file "plugins/KhSkins/players/%uuid of {_p}%.yml"
	{_m} = "main":
		{_selected} contains " : ":
			set {_family} to last element of {_selected} split at " : "
			set {_skin} to first element of {_selected} split at " : "
			loop yaml list "skins" from file "plugins/KhSkins/library.yml":
				loop-value contains first element of {_selected} split at " : "
				set {_pi} to loop-value
				set {_splll::*} to {_pi} split at " : "
				set {_selected} to {_splll::2}
				exit loop
		else:
			set {_family} to yaml value "options.no_gender" from "ks.skins.language"
			set {_skin} to {_selected}
		{_selected} is "none":
			set {_selected} to yaml value "options.without_selected" from "ks.skins.language"
			set {_skin} to "%{_p}%"
			getName({_p}) is "khskins->cracked":
				set {_skin} to yaml value "options.default_skin" from "ks.skins.language"
		set {_rows} to 4
		size of {_skins::*} is between 8 and 14:
			set {_rows} to 5
		size of {_skins::*} > 14:
			set {_rows} to 6
		set {_i} to 9*{_rows}-6
		open chest inventory with {_rows} rows named yaml value "menu.title" from "ks.skins.language" to {_p}
		wait 2 ticks
		set {_slot} to 10
		size of {_skins::*} > 0:
			loop {_skins::*}:
				set {_loop} to loop-value
				{_loop} is not ""
				{_selected} is {_loop}:
					set {_x} to replacer "{skin}" and "{state}" with {_loop} and yaml value "menu.replaces.selected" from "ks.skins.language" in yaml value "menu.icons.selected" from "ks.skins.language"
					make a gui slot {_slot} of {_p} with deserializeItem({_x}, {_p})
				else:
					set {_x} to replacer "{skin}" and "{state}" with {_loop} and yaml value "menu.replaces.select" from "ks.skins.language" in yaml value "menu.icons.select" from "ks.skins.language"
					make a gui slot {_slot} of {_p} with deserializeItem({_x}, {_p}) to run:
						"%click type%" contains "RIGHT":
							remove {_loop} from yaml list "%uuid of {_p}%.skins" from file "plugins/KhSkins/players/%uuid of {_p}%.yml"
							menu({_p})
						else:
							set yaml value "%uuid of {_p}%.select" from file "plugins/KhSkins/players/%uuid of {_p}%.yml" to {_loop}
							make console execute command "sr set %{_p}% %{_loop}%"
							close {_p}'s inventory
				add 1 to {_slot}
				{_slot} is 17 or 26:
					add 2 to {_slot}
		else:
			make a gui slot 13 of {_p} with deserializeItem(yaml value "menu.items.no_skins" from "ks.skins.language", {_p})
		set {_x} to replacer "{skin}" and "{selected}" and "{gende}" with {_skin} and {_selected} and {_family} in yaml value "menu.items.current_skin" from "ks.skins.language"
		make a gui slot {_i} of {_p} with deserializeItem({_x}, {_p}) to run:
			set yaml value "%uuid of {_p}%.select" from file "plugins/KhSkins/players/%uuid of {_p}%.yml" to "none"
			getName({_p}) is "khskins->cracked":
				set {_x} to yaml value "options.default_skin" from "ks.skins.language"
				make console execute "sr set %{_p}% %{_x}%"
			else:
				make console execute "sr set %{_p}% %{_p}%"
			menu({_p})
		make a gui slot {_i}+1 of {_p} with deserializeItem(yaml value "menu.items.update" from "ks.skins.language", {_p}) to run:
			{_selected} is yaml value "options.without_selected" from "ks.skins.language":
				set {_selected} to "%{_p}%"
				getName({_p}) is "khskins->cracked":
					set {_selected} to yaml value "options.default_skin" from "ks.skins.language"
			make console execute command "sr set %{_p}% %{_selected}%"
			menu({_p})
		make a gui slot {_i}+2 of {_p} with deserializeItem(yaml value "menu.items.library" from "ks.skins.language", {_p}) to run:
			menu({_p}, "library")
	{_m} = "library":
		libraryPages({_p})
function libraryPages(p: player, page: number = 0):
	set {_pageStart} to 21*{_page}
	set {_i} to 1
	set {_rows} to 4
	set {_slot} to 10
	set {_library::*} to yaml list "skins" from file "plugins/KhSkins/library.yml"
	set {_selected} to yaml value "%uuid of {_p}%.select" from file "plugins/KhSkins/players/%uuid of {_p}%.yml"
	size of {_library::*} is between 8 and 14:
		set {_rows} to 5
	size of {_library::*} > 14:
		set {_rows} to 6
	open chest inventory with {_rows} rows named yaml value "menu.title" from "ks.skins.library" to {_p}
	wait a tick
	loop {_library::*}:
		(loop-index parsed as integer) > {_pageStart}
		set {_loop} to loop-value
		set {_split::*} to {_loop} split at " : "
		first element of {_selected} split at " : " is {_split::1}:
			set {_x} to replacer "{skin}" and "{name}" and "{gende}" and "{state}" with {_split::1} and {_split::2} and {_split::3} and yaml value "menu.replaces.selected" from "ks.skins.library" in yaml value "menu.icon" from "ks.skins.library"
			make a gui slot {_slot} of {_p} with deserializeItem({_x}, {_p})
		else:
			set {_x} to replacer "{skin}" and "{name}" and "{gende}" and "{state}" with {_split::1} and {_split::2} and {_split::3} and yaml value "menu.replaces.select" from "ks.skins.library" in yaml value "menu.icon" from "ks.skins.library"
			make a gui slot {_slot} of {_p} with deserializeItem({_x}, {_p}) to run:
				set yaml value "%uuid of {_p}%.select" from file "plugins/KhSkins/players/%uuid of {_p}%.yml" to "%{_split::1}% : %{_split::3}%"
				make console execute command "sr set %{_p}% %{_split::1}%"
				close {_p}'s inventory
		add 1 to {_slot}
		{_slot} is 17 or 26:
			add 2 to {_slot}
		{_slot} = ((35*{_i})):
			exit loop
	(amount of {_library::*}) > {_pageStart} + 21:
		make a gui slot 9*{_rows}-1 of {_p} with deserializeItem(replacer "{next_page}" with "%{_page}+1+1%" in yaml value "options.pagged.next_page" from "ks.skins.language", {_p}) to run:
			libraryPages({_p}, ({_page} + 1))
	{_page} > 0:
		make a gui slot 9*{_rows}-9 of {_p} with deserializeItem(replacer "{previous_page}" with "%{_page}+1-1%" in yaml value "options.pagged.previous_page" from "ks.skins.language", {_p}) to run:
			libraryPages({_p}, ({_page} - 1))
	make a gui slot 9*{_rows}-5 of {_p} with deserializeItem(yaml value "options.pagged.back" from "ks.skins.language", {_p}) to run:
		wait a tick
		menu({_p})
# ─────────────────────────────────────────────────── #
function getName(p: offlineplayer) :: text:
	text from "https://api.mojang.com/users/profiles/minecraft/%{_p}%" != "":
		set {_x::*} to text from "https://api.mojang.com/users/profiles/minecraft/%{_p}%" split at """"
		return {_x::4}
	return "khskins->cracked"
function getStats(p: player):
	set {_u} to uuid of {_p}
	set {_f} to "plugins/KhSkins/players/%{_u}%.yml"
	fileExists({_f}) is not true:
		create file {_f}
		set yaml value "%{_u}%.name" from file "%{_f}%" to {_p}'s display name
		set yaml value "%{_u}%.select" from file "%{_f}%" to "none"
		add "" to yaml list "%{_u}%.skins" from file "%{_f}%"
		remove "" from yaml list "%{_u}%.skins" from file "%{_f}%"
		getName({_p}) is "khskins->cracked":
			make console execute replacer "{skin}" with yaml value "options.default_skin" from "ks.skins.language" in "sr set %{_p}% {skin}"

# ─────────────────────────────────────────────────── #
load:
	loop "language" and "library":
		fileExists("plugins/KhSkins/%loop-value%.yml") != true:
			create file "plugins/KhSkins/%loop-value%.yml"
			delete file "plugins/KhSkins/%loop-value%.yml"
			#download from "https://raw.githubusercontent.com/KickHolse/KhSkins/main/files/%loop-value%" to file "plugins/KhSkins/%loop-value%.yml"
		yaml "ks.skins.%loop-value%" is loaded:
			unload yaml "ks.skins.%loop-value%"
		load yaml "plugins/KhSkins/%loop-value%.yml" as "ks.skins.%loop-value%"
	text from "https://api.spigotmc.org/legacy/update.php?resource=495432" != "{@version}":
		send "[KhSkins] &cYou are not using the latest version of skript." to console
